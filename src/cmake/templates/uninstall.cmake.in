#=============================================================================
# SPDX-FileCopyrightText: Copyright (c) 2024-2026 NVIDIA CORPORATION & AFFILIATES. All rights reserved.
# SPDX-License-Identifier: Apache-2.0
#=============================================================================

if(NOT EXISTS "@INSTALL_MANIFEST_PATH@")
  message(FATAL_ERROR "Cannot find install manifest: @INSTALL_MANIFEST_PATH@")
endif()

function(prune_directory path)
  if(NOT EXISTS "${path}")
    # A previous call has already deleted the directory
    return()
  endif()

  file(GLOB dir_contents LIST_DIRECTORIES TRUE "${path}/*")
  list(LENGTH dir_contents len)
  if(len EQUAL 0)
    # Do this before we delete the directory
    cmake_path(GET path PARENT_PATH parent)

    message(STATUS "uninstalling  (dir): ${path}")
    file(REMOVE_RECURSE "${path}")

    prune_directory("${parent}")
  endif()
endfunction()

file(STRINGS "@INSTALL_MANIFEST_PATH@" files)

foreach(file IN LISTS files)
  cmake_path(SET file NORMALIZE "${file}")
  cmake_path(GET file PARENT_PATH parent)

  message(STATUS "uninstalling (file): ${file}")
  file(REMOVE "${file}")
  prune_directory("${parent}")
endforeach()
